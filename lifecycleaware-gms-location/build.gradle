buildscript {
  dependencies {
    classpath Libs.android_plugin
    classpath Libs.kotlin_plugin
    classpath Libs.dokka_plugin
  }
}

plugins {
  id "org.jlleitschuh.gradle.ktlint" version "4.1.0"
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

android {
  compileSdkVersion Vers.compile_sdk

  defaultConfig {
    minSdkVersion Vers.min_sdk
    targetSdkVersion Vers.target_sdk
    versionCode 1
    versionName "1.0"

    multiDexEnabled true // for test

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  libraryVariants.all {
    it.generateBuildConfig.enabled = false
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
  api project(':lifecycleaware')

  implementation Libs.kotlin

  implementation Libs.gms
  constraints {
    api(Libs.support_v4) {
      because "gms v15.0.0 depends to support lib 26.0.1"
    }
  }

  implementation Libs.viewmodel
  implementation Libs.livedata

  androidTestImplementation Libs.junit
  androidTestImplementation Libs.support_test
  androidTestImplementation Libs.espresso
  androidTestImplementation Libs.arch_test
  androidTestImplementation Libs.truth

  androidTestImplementation Libs.appcompat
}

ktlint {
  version = Vers.ktlint
  android = true
  reporters = ["CHECKSTYLE"]
  ignoreFailures = true
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
